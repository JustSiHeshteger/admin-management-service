plugins {
    id 'java'
    id 'org.springframework.boot' version '4.0.1'
    id 'io.spring.dependency-management' version '1.1.7'
    id "org.sonarqube" version "7.2.2.6593"
}

sonar {
    properties {
        property "sonar.projectKey", "JustSiHeshteger_admin-management-service"
        property "sonar.organization", "justsiheshteger"
    }
}


group = 'org.zvrg'
version = '0.0.1'
description = 'admin-management-service'

bootJar {
    archiveBaseName = 'admin-management-service'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

sourceSets {
    test {
        java {
            srcDir 'src/test/java'
        }
        resources {
            srcDir 'src/test/resources'
        }
        compileClasspath += sourceSets.main.output + configurations.testRuntimeClasspath
        runtimeClasspath += output + compileClasspath
    }
    integrationTest {
        java {
            srcDir 'src/integrationTest/java'
        }
        resources {
            srcDir 'src/integrationTest/resources'
        }
        compileClasspath += sourceSets.main.output + configurations.testRuntimeClasspath
        runtimeClasspath += output + compileClasspath
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'org.postgresql:postgresql'
    annotationProcessor 'org.projectlombok:lombok'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.postgresql:postgresql:42.7.8'
    implementation 'org.liquibase:liquibase-core:5.0.1'
    implementation 'com.mattbertolini:liquibase-slf4j:5.1.0'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
//    testImplementation "org.testcontainers:junit-jupiter"
//    testImplementation "org.testcontainers:postgresql"
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.register('integrationTest', Test) {
    description = 'Runs integration tests'
    group = 'verification'

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}

tasks.named('check') {
    dependsOn(tasks.named('integrationTest'))
}

tasks.register('printAppName') {
    doLast {
        println(tasks.bootJar.archiveBaseName.get())
    }
}